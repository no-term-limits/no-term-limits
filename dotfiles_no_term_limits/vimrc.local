set nocompatible " We're running Vim, not Vi!

" FIXME: hopefully real fix comes through in
" https://github.com/vim/vim/issues/3117 and this hack fix can be deleted.
" another hack fix is removing this plugin: https://github.com/editorconfig/editorconfig-vim/issues/108
if has('python3')
  silent! python3 1
endif

set et " expand tabs to spaces
set ts=2  " Tabs are 2 spaces
set sts=2 " softtabstop same as tabstop so hitting backspace deletes two spaces
set shiftwidth=2  " Tabs under smart indent
set bs=2  " Backspace over everything in insert mode
set ruler " Show the cursor position all the time, including the line number
set hidden " does something useful with hidden buffers
set hlsearch
set number
set noswapfile

" tmux on Ubuntu sets to light for some reason and messes up highlighting
set background=dark

" for vim-test. run tests (Leader+s for TestNearest, etc) using dispatch.vim
let test#strategy = "dispatch"

" Make Shift-K grep for word under cursor rather than look up docs
nnoremap K :grep! "\b<C-R><C-W>\b"<CR>:cw<CR>

let g:ctrlp_cmd = 'CtrlP .'
let g:ctrlp_regexp = 1

" Bubble (move) single lines
nmap <C-Up> [e
nmap <C-Down> ]e

" Bubble (move) multiple lines
vmap <C-Up> [egv
vmap <C-Down> ]egv

" Control
nmap <C-Left> :bp<cr>
nmap <C-Right> :bn<cr>

" allow playback of a macro without having to type the @ sign
nnoremap <silent> ,, @q

" If you're splitting windows with :sp and :vsp, hitting ctrl+w h will move
" you to the window to the left of the one you're in. it might be nicer
" to allow just ctrl+h to do that, eh?
nnoremap <C-h> <C-W>h
nnoremap <C-j> <C-W>j
nnoremap <C-k> <C-W>k
nnoremap <C-l> <C-W>l


" Leader mappings. Note that <leader> is the backslash key by default, and is
" overriden to be the Space key by thoughtbot/dotfiles

" fuzzy finding from fzf.vim. had been CtrlP.vim plugin
nnoremap <leader>f :Files<CR>

" buffer list via CtrlP
nnoremap <leader>b :Buffers<CR>

" search tabs similar to <leader>b
" nnoremap <leader>w :Windows<CR>

" Use the same symbols as TextMate for tabstops and EOLs
" Whoops i tried to make the tab work with insert mode ctrl+v u 25b8, but no dice
" http://vimcasts.org/episodes/show-invisibles/
set listchars=eol:$,tab:>-,trail:~,extends:>,precedes:<

if has("autocmd")
  filetype plugin indent on " Enable filetype detection, filetype-specific indenting, and filetype-specific plugins

  " Restore cursor position
  autocmd BufReadPost *
    \ if line("'\"") > 1 && line("'\"") <= line("$") |
    \   exe "normal! g`\"" |
    \ endif

  " http://stackoverflow.com/questions/2393671/vim-restores-cursor-position-exclude-special-files
  autocmd BufReadPost COMMIT_EDITMSG,svn-commit.tmp
    \ exe "normal! gg"

  " java
  " autocmd FileType java setlocal noexpandtab
  " autocmd Bufread,BufNewFile java setlocal noexpandtab
  autocmd FileType java,python setlocal shiftwidth=4
  autocmd FileType java,python setlocal tabstop=4
  autocmd FileType java,python setlocal softtabstop=4
  autocmd Bufread,BufNewFile java set list

  " git config
  autocmd Bufread,BufNewFile .gitconfig setlocal noexpandtab
  autocmd Bufread,BufNewFile .gitconfig set list

  " https://github.com/vim-scripts/Rainbow-Parentheses-Improved-and2
  let g:rainbow_active = 1
  let g:rainbow_operators = 1

  " Source the .vimrc file after saving it
  autocmd BufWritePost .vimrc source $MYVIMRC

  " generate tags for any user-generated docs when those docs are saved
  autocmd BufWritePost ~/.vim/doc/* :helptags ~/.vim/doc

  au bufNewFile,BufRead *.txt set spell

  " https://github.com/fatih/vim-go-tutorial#quick-setup.
  " <leader>b is, of course, taken
  " So i made <leader>w, <leader>n, and <leader>m, be useful
  autocmd FileType go nmap <leader>w  <Plug>(go-build)
  autocmd FileType go nmap <leader>n  :cnext<CR>
  autocmd FileType go nmap <leader>m  :cprevious<CR>
  autocmd FileType go nmap <leader>x  :cclose<CR>
  autocmd FileType go nmap <leader>d  <Plug>(go-def)

  " strip trailing whitespace when the file types in the BufWritePre line
  " below. reference: https://unix.stackexchange.com/a/75438/456630
  function! <SID>StripTrailingWhitespaces()
    let l = line(".")
    let c = col(".")
    %s/\s\+$//e
    call cursor(l, c)
  endfun
  autocmd BufWritePre * if &ft =~ '^\(ruby\|sh\|zsh\|javascript\)$' | :call <SID>StripTrailingWhitespaces() | endif

endif

" 'quote' (single quote) a word. otherwise, ysiw'
nnoremap qw :silent! normal mpea'<Esc>bi'<Esc>`pl
" remove quotes from a word
nnoremap wq :silent! normal mpeld bhd `ph<CR>
nnoremap qu :s#\(\<\u\l\+\|\l\+\)\(\u\)#\l\1_\l\2#g<CR>

nmap <F8> :TagbarToggle<CR>

if has("gui_running")
  " work around linux gui F10 problem by making it a nice safe refresh
  if has("gui_gnome") || has("gui_gtk") || has("gui_gtk2")
    map <F10> <c-L>
  endif
endif

syntax on " Enable syntax highlighting

" highlights the whole line just for quickfix
" would rather have this in a custom buffer
au BufReadPost quickfix setlocal cursorline

command! -nargs=* -complete=file T :tabnew <args>

" Overrides for dotfiles setup
set nolist
set colorcolumn=0
set textwidth=0

" SpellCap was unreadable on iterm2 with 256 colors. underline instead of highlight is better anyway.
hi clear SpellCap
hi link SpellCap Underlined

" https://thoughtbot.com/upcase/videos/tmux-vim-integration#final-configuration
" automatically rebalance windows on vim resize
autocmd VimResized * :wincmd =

" copy the full file path into the system clipboard
nmap cp :let @+ = expand("%:p")<cr>

if has("autocmd")
  autocmd BufRead,BufNewFile *.thrift set filetype=thrift

  " java
  " autocmd FileType java setlocal noexpandtab
  " autocmd Bufread,BufNewFile java setlocal noexpandtab
  autocmd FileType java,python setlocal shiftwidth=4
  autocmd FileType java,python setlocal tabstop=4
  autocmd FileType java,python setlocal softtabstop=4
  autocmd Bufread,BufNewFile java set list

  au BufReadPost Jenkinsfile set syntax=groovy
  au BufReadPost Jenkinsfile set filetype=groovy
  au FileType groovy setlocal expandtab
endif

" reactjs stuff
" mostly from https://speakerdeck.com/jwworth/react-dot-js-plus-vim
augroup fmt
  autocmd!
  " set up autocorrect just for js and jsx files in trove and tully-tokenizer.
  " and fe (frontend).
  " we originally had ImportJSFix before Neoformat, and had it on two lines.
  " Neoformat actual sets -bar, so the bar (pipe) works in this order. i'm not
  " sure the order is optimal, but at least it works on one line, which i
  " suspect will help with undojoin
  "
  " FIXME:
  " autocmd BufWritePre *projects/fe/*.js*,*trove/*.js*,*tully-tokenizer/*.js* undojoin | Neoformat | ImportJSFix
augroup END

" react
let g:mta_filetypes = {
      \ 'javascript.jsx': 1,
      \}
" end reactjs stuff

fun! Should_execute_t_file(t_file_to_use)
  if filereadable(a:t_file_to_use) && match(readfile(a:t_file_to_use),"DISABLE_T_FILE") == -1
    if localtime() - getftime(a:t_file_to_use) < 86400
      return 1
    endif
  endif
  return 0
endfun

" this is pretty intense. it's meant to help speed up a workflow. so first
" make sure you are running vim inside tmux all day every day.
" i assume i lost most everyone.
" then split your tmux window into multiple panes. maybe like 4 panes
" split in quadrants. open vim in maybe the bottom left pane (anywhere but
" bottom right). make sure your working directory in the bottom left pane
" (vim) is the same as that in the bottom right (where you should have an
" empty shell). Now create a file in the working directory called t. Put any
" shell command in there that you want. When you save any file in vim, that
" command the "t file" will be run in the bottom right tmux pane. It's not
" perfect, but it's pretty magical for certain workflows, like editing code in
" one pane and running it automatically in another.
fun! Run_run_in_tmux_pane_ProgramInTmuxPane() "{{{
  let file_that_was_changed = '<afile>'
  " if you have a file called "t", it will run to the right, since t is short and right is best!
  if Should_execute_t_file('t')
    execute 'silent !tmux send-keys -t bottom-right "run_in_tmux_pane ' . getcwd() . '/t '. file_that_was_changed . '" Enter'
    redraw!
  endif
  if Should_execute_t_file('t_right')
    execute 'silent !tmux send-keys -t bottom-right "run_in_tmux_pane ' . getcwd() . '/t_right '. file_that_was_changed . '" Enter'
    redraw!
  endif
  if Should_execute_t_file('t_bottom')
    execute 'silent !tmux send-keys -t bottom "run_in_tmux_pane ' . getcwd() . '/t_bottom '. file_that_was_changed . '" Enter'
    redraw!
  endif
endfunction "}}}
autocmd BufWritePost * cal Run_run_in_tmux_pane_ProgramInTmuxPane()

" Some folks are not huge fans of the "Get off my lawn" settings in thoughtbot dotfiles
nnoremap <Left> <Left>
nnoremap <Right> <Right>
nnoremap <Up> <Up>
nnoremap <Down> <Down>

fun! Add_Shebang_line() "{{{
  execute "normal! ggi#!/usr/bin/env bash\<cr>\<cr>set -eo pipefail\<cr>\<cr>"
endfunction "}}}

nnoremap <leader>rs :cal Add_Shebang_line()<cr>

fun! OpenLastFailedTestFile() "{{{
  let file_to_open = system('get_last_failed_test_file')
  call feedkeys(":e " . file_to_open)
endfun "}}}

command! OpenLastFailedTestFile call OpenLastFailedTestFile()

" disable window functionality for fzf because it causes the cursor to move to
" the top of the current window whenever using ctrl-p which is really annoying
"
" FROM https://github.com/junegunn/fzf commit c60ed1758315f0d993fbcbf04459944c87e19a48
"     If you prefer the previous default, add this to your Vim configuration file:
"       let g:fzf_layout = { 'down': '40%' }
"     (fzf will fall back to this if popup window is not supported)
let g:fzf_layout = { 'down': '40%' }


" remove mappings from thoughtbot/dotfiles
if ! empty(maparg('<leader>t', 'n'))
nunmap <silent> <leader>t
endif
if ! empty(maparg('<leader>s', 'n'))
nunmap <silent> <leader>s
endif
if ! empty(maparg('<leader>l', 'n'))
nunmap <silent> <leader>l
endif
if ! empty(maparg('<leader>a', 'n'))
nunmap <silent> <leader>a
endif
if ! empty(maparg('<leader>gt', 'n'))
nunmap <silent> <leader>gt
endif
if ! empty(maparg('<leader>r', 'n'))
nunmap <silent> <leader>r
endif
if ! empty(maparg('\', 'n'))
nunmap \
endif

" added by tabnine-vim/YouCompleteMe
let g:ycm_key_detailed_diagnostics=''

" added by tComment / tcomment_vim
let g:tcommentMapLeader1=''
let g:tcomment_mapleader1=''

" default mapping from ctags in favor of "+run" menu, "run ctags", <leader>rc
" unmap <silent!> <leader>ct
" nmap <leader>ct :call ReindexCtags()<cr>
let g:thoughtbot_ctags_mappings_enabled = 0

" vim-which-key plugin
" nnoremap <silent> <leader> :WhichKey '<Space>'<cr>
nnoremap <silent> <leader> :silent WhichKey '<Space>'<cr>
vnoremap <silent> <leader> :silent <c-u> :silent WhichKeyVisual '<Space>'<cr>
nnoremap <silent> <localleader> :silent WhichKey '\'<cr>
vnoremap <silent> <localleader> :silent <c-u> :silent WhichKeyVisual '\'<cr>

" vim-jenkins
let g:jenkins_enable_mappings = 0

" credit to https://www.chrisatmachine.com/Neovim/15-which-key/ for initial
" vim-which-key configs.
"
" Create map to add keys to
let g:which_key_map =  {}

let g:which_key_localleader_map =  {}

" Define a separator
let g:which_key_sep = 'â†’'

" defaults to 1000
set timeoutlen=600

" Not a fan of floating windows for this
let g:which_key_use_floating_win = 0

" Change the colors if you want
highlight default link WhichKey          Operator
highlight default link WhichKeySeperator DiffAdded
highlight default link WhichKeyGroup     Identifier
highlight default link WhichKeyDesc      Function

" Hide status line
autocmd! FileType which_key
autocmd  FileType which_key set laststatus=0 noshowmode noruler
  \| autocmd BufLeave <buffer> set laststatus=2 noshowmode ruler

" Define prefix dictionary
let g:which_key_map =  {}

" Single mappings

let g:which_key_map['s'] = [ ':ALEToggle'                   , 'toggle ale' ]
" let g:which_key_map['/'] = [ '<Plug>NERDCommenterToggle'  , 'comment' ]
" let g:which_key_map['e'] = [ ':CocCommand explorer'       , 'explorer' ]
let g:which_key_map['f'] = [ ':Files'                       , 'search files' ]
let g:which_key_map['b'] = [ ':Buffers'                     , 'search open buffers' ]
" let g:which_key_map['h'] = [ '<C-W>s'                     , 'split below']
" let g:which_key_map['r'] = [ ':Ranger'                    , 'ranger' ]
" let g:which_key_map['S'] = [ ':Startify'                  , 'start screen' ]
" let g:which_key_map['T'] = [ ':Rg'                        , 'search text' ]
" let g:which_key_map['v'] = [ '<C-W>v'                     , 'split right']
" let g:which_key_map['z'] = [ 'Goyo'                       , 'zen' ]

" s is for search
let g:which_key_map.s = {
      \ 'name' : '+search' ,
      \ '/' : [':History/'     , 'history'],
      \ ';' : [':Commands'     , 'commands'],
      \ 'b' : [':BLines'       , 'current buffer'],
      \ 'B' : [':Buffers'      , 'open buffers'],
      \ 'C' : [':BCommits'     , 'buffer commits'],
      \ 'f' : [':Files'        , 'files'],
      \ 'g' : [':GFiles'       , 'git files'],
      \ 'G' : [':GFiles?'      , 'modified git files'],
      \ 'h' : [':History'      , 'file history'],
      \ 'H' : [':History:'     , 'command history'],
      \ 'l' : [':Lines'        , 'lines'] ,
      \ 'm' : [':Marks'        , 'marks'] ,
      \ 'M' : [':Maps'         , 'normal maps'] ,
      \ 'p' : [':Helptags'     , 'help tags'] ,
      \ 'P' : [':Tags'         , 'project tags'],
      \ 's' : [':Snippets'     , 'snippets'],
      \ 'S' : [':Colors'       , 'color schemes'],
      \ 't' : [':Rg'           , 'text Rg'],
      \ 'T' : [':BTags'        , 'buffer tags'],
      \ 'w' : [':Windows'      , 'search windows'],
      \ 'y' : [':Filetypes'    , 'file types'],
      \ 'z' : [':FZF'          , 'FZF'],
      \ }

nnoremap <leader>sa :Ag<Space>
let g:which_key_map.s.a = 'text Ag'
nnoremap <leader>sc :Commits<cr>
let g:which_key_map.s.c = 'commits'

let g:which_key_map.v = {
      \ 'name' : '+vimrc' ,
      \ 'v' : [':edit $MYVIMRC'                                                    , '(vimrc, loaded #1) edit $MYVIMRC (thoughtbot dotfiles, ~/.vimrc)'],
      \ 'n' : [':edit ~/.vimrc.local'                                              , '(no-term, #2) edit ~/.vimrc.local'],
      \ 'o' : [':edit ~/.vimrc.no_term_limits_overrides'                           , '(overrides of no-term, #3) edit ~/.vimrc.no_term_limits_overrides'],
      \ 's' : [':source $MYVIMRC'                                                  , 'source $MYVIMRC'],
      \ }

let g:which_key_map.t = {
      \ 'name' : '+toggle' ,
      \ 'a' : [':ALEToggle'     , 'toggle ale linting on/off'],
      \ 'h' : [':set hlsearch!' , 'toggle highlighted search'],
      \ 'i' : [':IndentLinesToggle' , 'toggle IndentLines'],
      \ 'l' : [':set list!'     , 'toggle list mode'],
      \ 'p' : [':set paste!'    , 'toggle paste mode'],
      \ }

let g:CheatSheetDoNotMap=1

" regarding ce CheatError and the someday comment:
" see https://github.com/dbeniamine/cheat.sh-vim#errors about adding a provider and:
" https://github.com/dense-analysis/ale/issues/1810#issuecomment-419708225
let g:which_key_map.c = {
      \ 'name' : '+cheat.sheet' ,
      \ 'c' : ['CheatSheetQueryToBuffer()'     , 'query cheat.sheet for current language'],
      \ 'e' : [':CheatError'     , 'look up Error from syntastic and maybe someday ale'],
      \ 'w' : [':call CheatSheetEntryToBuffer()'     , 'get cheat.sheet entry for current Word'],
      \ 'r' : [':call CheatSheetSelectEntryFromRosettaCode()'     , 'select rosetta code cheat.sheet entry'],
      \ 'b' : [':bd _cheat' , 'close _cheat Buffer'],
      \ 'j' : [":call cheat#navigate(1,'Q')<CR>"     , 'next question'],
      \ 'k' : [":call cheat#navigate(-1,'Q')<CR>"    , 'prev question'],
      \ 'l' : [":call cheat#navigate(1,'A')<CR>"    , 'next answer'],
      \ 'h' : [":call cheat#navigate(-1,'A')<CR>"     , 'prev answer'],
      \ }

fu! CheatSheetSelectEntryFromRosettaCode()
  let l:filetypeforcheat = cheat#frameworks#getFt()
  let l:wordundercursor = expand('<cword>')
  " sink is where the result from fzf goes. in this case it goes to a function.
  call fzf#run({'source': 'cheat_sheet_entry_for_lang ' . l:filetypeforcheat, 'sink': function('CheatSheetOpenEntryForLang')})
endfunction

fu! CheatSheetOpenEntryForLang(entry_from_fzf)
  " Cheat only accepts one slash and the rest is ignored, so
  " rust/rosetta/Tree-traversal doesn't work that great.
  " execute 'Cheat rust/rosetta-what/' . a:entry_from_fzf
  let l:filetypeforcheat = cheat#frameworks#getFt()
  execute 'Cheat '. l:filetypeforcheat .'/' . a:entry_from_fzf
endfunction

fu! CheatSheetQueryToBuffer()
  let l:filetypeforcheat = cheat#frameworks#getFt()
  let query = input('Enter ' . l:filetypeforcheat . ' query: ')
  if ! empty(query)
    call cheat#cheat(l:filetypeforcheat . "/" . query, 0, 0, 0, 0, '!')
  endif
endfunction

" this takes the word under the cursor and opens up the
" corresponding cheat sheet entry (rather than doing a query, which is likely
" to return stackoverflow answers)
fu! CheatSheetEntryToBuffer()
  let l:filetypeforcheat = cheat#frameworks#getFt()
  let l:wordundercursor = expand('<cword>')
  execute 'Cheat ' . l:filetypeforcheat . ' ' . l:wordundercursor
endfunction

" when using cht.sh plugin (https://github.com/dbeniamine/cheat.sh-vim),
" this will remap gf inside the _cheat buffer so you can open the shortened
" stackoverflow links (that look like [so/q/56148586]) in a browser.
function! <SID>GfForCheatShBufferInCaseStackoverflowLink()
  let line = getline('.')
  let stackoverflow_pattern = '\[so\/q\/\d\+\]'
  if line =~ stackoverflow_pattern
    let matched_thing = matchstr(getline('.'), stackoverflow_pattern)
    let matched_thing = substitute(matched_thing, '[so/q/', 'https://stackoverflow.com/questions/', 'g')
    let matched_thing = substitute(matched_thing, ']', '', '')
    silent exec "!open_url '". matched_thing ."'"
    redraw!
  endif
endfun

autocmd BufRead,BufNewFile */_cheat map <buffer> gf :call <SID>GfForCheatShBufferInCaseStackoverflowLink()<cr>

" this is broken after :LspInstallServer typeprof:
" BUNDLE_GEMFILE=~/.local/share/vim-lsp-settings/servers/typeprof/Gemfile bundle exec ruby ~/.local/share/vim-lsp-settings/servers/typeprof/exe/typeprof certain-files.rb
let g:lsp_settings = {
\  'typeprof': {
\    'disabled': 1,
\   }
\}

" START GENERATE UUID FUNCTION. this cannot be indented properly. :D
" credit to https://gist.github.com/jonmorehouse/8442341
fu! GenerateUUID()

py3 << EOF
import uuid
import vim

# output a uuid to the vim variable for insertion below
vim.command("let generatedUUID = \"%s\"" % str(uuid.uuid4()))

EOF

" insert the python generated uuid into the current cursor's position
:execute "normal i" . generatedUUID . ""
endfunction
" END GENERATE UUID FUNCTION

"initialize the generateUUID function here and map it to a local command
noremap <Leader>rg :call GenerateUUID()<CR>

" Ignores stuff in ~/dotfiles/vim/plugin/ctags.vim
nmap <leader>rc :!ctags -R --exclude=.mypy_cache --exclude=.nox<cr>
let g:which_key_map.r = { 'name' : '+run' }
let g:which_key_map.r.c = 'run ctags'

nmap <leader>rd :%s/\s\+$//e<cr>
let g:which_key_map.r.d = 'delete trailing whitespace'

nnoremap <leader>rj :JenkinsValidateJenkinsFile<cr>
let g:which_key_map.r.j = 'run Jenkinsfile lint'
nnoremap <leader>rr :!open_url "https://www.youtube.com/watch?v=dQw4w9WgXcQ"<cr>
nnoremap <leader>rt :TestNearest<cr>
let g:which_key_map.r.r = 'run rickroll'
let g:which_key_map.r.s = 'run add shebang line'
let g:which_key_map.r.t = 'run tests'
let g:which_key_map.r.g = 'run guid generator'

" added by vim-rubocop plugin, so just documenting here. can be switched off by user with: let g:vimrubocop_keymap = 0
let g:which_key_map.r.u = 'run rubocop'
nnoremap <leader>r7 :OpenLastFailedTestFile<cr>
let g:which_key_map.r.7 = 'OpenLastFailedTestFile'

" actually creating these mappings in which-key results in mappings that don't
" work. so define them normally and document them with which key per below.
" let g:which_key_map.p = {
"       \ 'name' : '+panes' ,
"       \ 'z' : [':wincmd _<cr>:wincmd \|<cr>'    , 'zoom vim'],
"       \ 'r' : [':wincmd =<cr>'                  , 're-balance vim'],
"       \ }
let g:which_key_map.p = { 'name' : '+panes' }
nnoremap <leader>pz :wincmd _<cr>:wincmd \|<cr>
let g:which_key_map.p.z = 'zoom vim'
nnoremap <leader>pr :wincmd =<cr>
let g:which_key_map.p.r = 're-balance vim'

nnoremap <leader>pz :wincmd _<cr>:wincmd \|<cr>

" these make it really easy to open several files from the same directory:
cnoremap %% <C-R>=expand('%:h').'/'<cr>
map <localleader>ew :e %%
map <localleader>es :sp %%
map <localleader>ev :vsp %%
map <localleader>et :tabe %%
let g:which_key_localleader_map.e = { 'name' : '+edit' }
let g:which_key_localleader_map.e.w = 'open file in same directory'

" tcomment_vim mappings, meh
let g:which_key_map['_'] = { 'name': 'which_key_ignore' }

" FIXME: remove once https://github.com/thoughtbot/dotfiles/pull/684 is merged
" but for now, ignore the ctags mapping on ct
" UPDATE: now being used for cheat sheet shortcuts (see above)
" let g:which_key_map.c = { 'name': 'which_key_ignore' }

" from https://github.com/stsewd/fzf-checkout.vim
nnoremap <leader>gc :GBranches<cr>

" vim-fugitive
nnoremap <leader>gs :G<cr>

" after running git status and entering 3-column merge UI with dv (diff
" vertical, shortcut for :Gvdiffsplit), use one of these to resolve the
" conflict in each chunk. the f and j don't stand for anything. they are just
" on the left and right on a qwerty keyboard. credit to theprimeagen for the
" mapping idea.
nnoremap <leader>gf :diffget //2<cr>
nnoremap <leader>gj :diffget //3<cr>

" vim-gitgutter
let g:gitgutter_map_keys = 0

" git undo (hunk)
nmap <Leader>gu <Plug>(GitGutterUndoHunk)

let g:which_key_map.g = { 'name': '+git' }
let g:which_key_map.g.c = 'git checkout w/ fzf'
let g:which_key_map.g.s = 'git status'
let g:which_key_map.g.f = 'git resolve conflict left'
let g:which_key_map.g.j = 'git resolve conflict right'
let g:which_key_map.g.u = 'git undo hunk'

" Register which key map
call which_key#register('<Space>', "g:which_key_map")
call which_key#register('\', "g:which_key_localleader_map")

function! OpenCurrentCodeLineInBrowser()
  let line_number=line('.')
  let file_full_path=bufname("%")
  execute "! git_open " . file_full_path . " " . line_number
endfunction

" use calling <cr> twice so it returns back to vim after opening the link
nnoremap <Leader>rb :call OpenCurrentCodeLineInBrowser()<cr><cr>
let g:which_key_map.r.b = 'open source file in browser'

" https://stackoverflow.com/questions/14896327/ultisnips-and-youcompleteme
" so you can hammer "rwb<ctrl>j>" instead of "rwb<tab>", which does not work
let g:UltiSnipsExpandTrigger="<c-j>"
let g:UltiSnipsListSnippets="<c-k>"

" Trigger a highlight in the appropriate direction when pressing these keys:
let g:qs_highlight_on_keys = ['f', 'F', 't', 'T']

" Fugitive .git/tags support removed in favor of `:set tags^=./.git/tags;`
set tags^=./.git/tags


function! RunMakeAndMaybeOpenQuickfixWindow()
  execute "silent make"
  if empty(getqflist())
    cclose
  else
    copen
  endif
  redraw!
endfunction

" run :make in a python file to do mypy
autocmd Filetype python set makeprg=MYPYPATH=$PWD/src\ poetry\ run\ mypy\ src\ tests\ docs/conf.py\ --no-pretty\ --no-color-output
" if there are issues, open quickfix window. close with :cclose. next issue with :cnext.
autocmd QuickFixCmdPost [^l]* cwindow

" quickfix shortcuts
" ma for make. mm since it's the most important so make it easy to type. mc for make close. mo for make open.
" Linting your code, the vanilla way: https://gist.github.com/romainl/ce55ce6fdc1659c5fbc0f4224fd6ad29
autocmd FileType python nmap <leader>mm  :cnext<CR>
autocmd FileType python nmap <leader>mp  :cprevious<CR>
autocmd FileType python nmap <leader>ma  :make<CR>
autocmd FileType python nmap <leader>mc  :cclose<CR>
autocmd FileType python nmap <leader>mo  :cclose<CR>

" run :make to compile a java class
autocmd Filetype java set makeprg=javac\ %
autocmd Filetype java set errorformat=%A%f:%l:\ %m,%-Z%p^,%-C%.%#
autocmd Filetype java map <leader>ra :call RunMakeAndMaybeOpenQuickfixWindow()<cr>
autocmd Filetype java map <leader>rr :call RunJavaClass()<cr>
autocmd Filetype java map <F6> :cprevious<Return>
autocmd Filetype java map <F7> :cnext<Return>
" nnoremap M :silent make\|redraw!\|cw\|silent cc<CR>\|call RunJavaClass()<cr>

function! SearchForLinesContainingPattern(pattern)
    let line1 = search(a:pattern)
    return getbufline(bufnr('%'), line1, line1)
endfunction

" command! -bang -nargs=* -complete=file Make AsyncRun -program=make @ <args>

func! RunJavaClass()
  " Dispatch " runs make by default
  " Make " would use commented out command! above
  " if you RunMakeAndMaybeOpenQuickfixWindow, it doesn't show you the output of the program when it
  " runs, so compile and run in separate commands.
  " call RunMakeAndMaybeOpenQuickfixWindow()
  "
  let lines = SearchForLinesContainingPattern("^package")
  let numberOfPackageParts = 0
  let package = ''
  let javaClassName = expand('%:p:t:r')

  if !empty(lines)
    let line = lines[0]
    let package = substitute(line, 'package ', '', '')
    let package= substitute(package, ';', '', '')
    let numberOfPackageParts = count(package, '.') + 1
  endif

  let ii = 0
  let filenameModifiers = '%:p:h'
  while ii < numberOfPackageParts
    " get the head of the filename (parent dir) as many times as necessary
    let filenameModifiers = filenameModifiers . ':h'
    let ii += 1
  endwhile

  let directoryForClassPath = expand(filenameModifiers)
  let javaClassName = expand('%:p:t:r')
  if empty(package)
    let packageAndClassName = javaClassName
  else
    let packageAndClassName = package . '.' . javaClassName
  endif

  " echo '!java -cp ' . directoryForClassPath . ' ' . packageAndClassName
  execute '!java -cp ' . directoryForClassPath . ' ' . packageAndClassName
endfunc

command! Java call RunJavaClass()

" set statusline
" resources:
"   * https://learnvimscriptthehardway.stevelosh.com/chapters/17.html
"   * https://stackoverflow.com/questions/33033646/how-to-show-constantly-current-working-directory-in-vim
" ending in space for padding on the right side
hi fullPathHi gui=none cterm=bold ctermbg=black ctermfg=blue
hi fileHi gui=none cterm=bold ctermbg=black ctermfg=green
hi mainHi gui=none cterm=bold ctermbg=black ctermfg=white
set statusline=%#fullPathHi#%{getcwd()}/%#fileHi#%f%m%#mainHi#\ %y%=%l,%c\ Total:\ %L\ 

if filereadable($HOME . "/.vimrc.no_term_limits_overrides")
  source ~/.vimrc.no_term_limits_overrides
endif

" https://aloha.org.cn/2021/03/28/Create-Snippets-for-Ultisnips/ with vimscript
" regex explanation:
" turn on very magic. match optional two characters. match any number of
" word characters and capture them. match an equal sign and number-related
" variable on the right. these three should all match if found at the
" beginning of the line and find the name on the left:
" something="$1"
" another="${2}"
" stillanother="${3:-}"
func! SnippetAddUsageLineToBashScript()
  let regexForVariableAssignments = '\v^(\w+)\="\$\{?(\d)(:-)?\}?"'
  let argVariableAssignmentLineNumbers=getline(1, '$')->map({l, v -> [l+1, v =~ regexForVariableAssignments]})->filter({k,v -> v[1]})->map({k,v -> v[0]})
  let biggestArgumentNumber = 0
  let variableNames = []
  for lineNumber in argVariableAssignmentLineNumbers
    let textOnLine = getbufline(bufname(), lineNumber)
    let matchlist = matchlist(textOnLine, regexForVariableAssignments)
    let variableName = matchlist[1]
    let variableNumber = matchlist[2]
    if variableNumber > biggestArgumentNumber
      let biggestArgumentNumber = variableNumber
    endif
    call add(variableNames, variableName)
  endfor
  let usageLine = '  >&2 echo "usage: $(basename "$0")'
  for variableName in variableNames
    let usageLine = usageLine . ' [' . variableName . ']'
  endfor
  let usageLine = usageLine . '"'

  let firstLine = 'if [[ -z "${' . biggestArgumentNumber . ':-}" ]]; then'
  let lines = [firstLine, usageLine, '  exit 1', 'fi']
  " setline (change line) and append (append after line) don't work as well since ultisnips wants to use the return value,
  " which is going to be 0 or 1 if you return nothing.
  " call setline('.', getline('.') . firstLine)
  " call append(line('.'), lines)
  return join(lines, "\n")
endfunc

let g:which_key_map.l = { 'name' : '+language server protocol (lsp)' }
if has('nvim')
  nnoremap <leader>lf :lua vim.lsp.buf.formatting()<cr>
  let g:which_key_map.l.f = 'lsp format file'

  nnoremap <leader>ld :lua vim.lsp.buf.definition()<cr>
  let g:which_key_map.l.d = 'lsp definition'

  nnoremap <leader>le :lua vim.diagnostic.open_float({scope="line"})<cr>
  let g:which_key_map.l.e = 'lsp error'

  nnoremap <leader>ls :LspInfo<cr>
  let g:which_key_map.l.s = 'lsp info (status)'

  nnoremap <leader>la :lua vim.lsp.buf.code_action()<cr>
  let g:which_key_map.l.a = 'lsp code action'
else
  nnoremap <leader>ld :LspDefinition<cr>
  let g:which_key_map.l.d = 'lsp definition'
  nnoremap <leader>li :LspInstallServer<cr>
  let g:which_key_map.l.i = 'install lsp server'
  nnoremap <leader>ls :LspStatus<cr>
  let g:which_key_map.l.s = 'lsp status'

  for standard_vim_plugin_to_plug in g:standard_vim_plugins_ftw
    " prabirshrestha/vim-lsp becomes vim-lsp
    let repo_without_org = substitute(standard_vim_plugin_to_plug, '.\{-}/', "", "")
    call plug#load(repo_without_org)
  endfor
endif

" the huge red vertical line at 79 characters when .editorconfig says max_line_length = 79
" is quite distracting and hard to style. you can't make it thinner, for one.
" so turn it off. editorconfig was basically doing set colorcolumn=80 in that
" case.
let g:EditorConfig_max_line_indicator = "none"
